:Namespace Utils

      pget←{
          ⍝ Get input of an exercise of Advent Of Code
          ⍝ Put that text in {pnum}_{part}.txt in {year}/input folder
          (year pnum part) ← ⍕¨⍵
          path ← year,'\input\',pnum,'_',part,'.txt'
          ⎕NEXISTS path: ⊃⎕NGET path 1 ⍝ If the file exist, just read it
          cookie  ← '_gid=<>;'  ⍝ Put cookie's value instead of '<>'
          cookie ,← 'session=<>;'
          cookie ,← '_ga=<>'
          url←'https://adventofcode.com/',year,'/day/',pnum,'/input'
          resp←HttpCommand.Get url''('cookie'cookie)
          ⊃⎕NGET path 1⊣resp.Data ⎕NPUT path
      }
      
    ints←⍎¨⎕D∘(∊⍨⊆⊢)
    sints←⍎¨(⎕D,'-')∘(∊⍨⊆⊢)
    chars←(⎕A,⎕C ⎕A)∘(∊⍨⊆⊢)
    spaces←' '∘(≠⊆⊢)

    blanks←⊢⊆⍨0≠≢¨
    merge←1↓' '∘((⊃,/)⍤,,⍤0)
    format←merge ¨blanks
    
    dirs←(⊢⍪-)∘.=⍨⍳2
    g←⍬
    mkgraph←{ns←⍸'#'≠⍵ ⋄ g⊢←{(⎕IO+≢ns)~⍨ns⍳cs←↓dirs+⍤1⊢⍵}¨ns}

:EndNamespace
